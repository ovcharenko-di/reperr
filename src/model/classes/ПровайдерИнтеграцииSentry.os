#Использовать 1connector
#Использовать logos

// DSN
Перем DSN;

// Логгер 
Перем Логгер;

Перем BASE_URI;
Перем PUBLIC_KEY;
Перем PROJECT_ID;

Перем Лог;

Перем HTTP_CODE_OK_200;
Перем LF;

Процедура ПриСозданииОбъекта()
	
	ФайлЖурнала = Новый ВыводЛогаВФайл;
	ФайлЖурнала.ОткрытьФайл("/var/log/reperr.integrations.sentry.log");

	Лог = Логирование.ПолучитьЛог("reperr.integrations.sentry");
	Лог.ДобавитьСпособВывода(ФайлЖурнала);

КонецПроцедуры

Функция DSN(вхDSN) Экспорт
	
	DSN = вхDSN;

	// https://develop.sentry.dev/sdk/overview/#parsing-the-dsn
	//
	// '{PROTOCOL}://{PUBLIC_KEY}:{SECRET_KEY}@{HOST}{PATH}/{PROJECT_ID}'
	//
	//  {BASE_URI} = '{PROTOCOL}://{HOST}{PATH}'

	URI = КоннекторHTTP.РазобратьURL(DSN);

	LastSlash = СтрНайти(URI.Путь, "/", НаправлениеПоиска.СКонца);
	PATH = Лев(URI.Путь, LastSlash - 1);
	PROJECT_ID = Сред(URI.Путь, LastSlash + 1);

	BASE_URI = URI.Схема + "://" + URI.Сервер + ":" + URI.Порт;
	Если Не ПустаяСтрока(PATH) Тогда
		BASE_URI = BASE_URI + "/" + PATH;
	КонецЕсли;
	PUBLIC_KEY = URI.Аутентификация.Пользователь;

	Возврат ЭтотОбъект;
	
КонецФункции

Функция Логгер(вхЛоггер) Экспорт
	
	Логгер = вхЛоггер;
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ОтправлятьКаждыйОтчет() Экспорт

	Возврат Истина; 

КонецФункции

Функция ЗарегистрироватьОшибку(ДанныеОтчетаОбОшибке) Экспорт

	ТелоЗапроса = СформироватьТелоЗапроса(ДанныеОтчетаОбОшибке);

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	ЗаголовокАутентификации = СтрШаблон("Sentry sentry_version=7,sentry_key=%1", КлючAPI);
	Заголовки.Вставить("X-Sentry-Auth", ЗаголовокАутентификации);

	РезультатЗапроса = КоннекторHTTP.Post(URL, ТелоЗапроса, , Новый Структура("Заголовки", Заголовки));
	
	Если РезультатЗапроса.КодСостояния <> 200 Тогда
		ВызватьИсключение("Не удалось отправить событие в Sentry"); // TODO: сделать внятное сообщение
	КонецЕсли;

	POST_ENDPOINT("/store/", ТелоЗапроса);

	Если IsSystemCrash(ДанныеОтчетаОбОшибке) Тогда
		Минидамп = НайтиМинидамп(ДанныеОтчетаОбОшибке);
		Скриншот = ДанныеОтчетаОбОшибке.Скриншот;

		POST_ENDPOINT("/minidump/", ТелоЗапроса, Скриншот, Минидамп);
	Иначе
		POST_ENDPOINT("/store/", ТелоЗапроса);
	КонецЕсли;

	ОтправитьФидбэк(ДанныеОтчетаОбОшибке);
	ОтправитьФайлы(ДанныеОтчетаОбОшибке);

	// В Sentry отправляется каждая ошибка
	Возврат Неопределено;
	
КонецФункции

Функция IsSystemCrash(ДанныеОтчетаОбОшибке)

	Отчет = ДанныеОтчетаОбОшибке.Отчет;
	Возврат Отчет["errorInfo"]["systemErrorInfo"]["systemCrash"] = Истина; 

КонецФункции

Функция НайтиМинидамп(ДанныеОтчетаОбОшибке)

	ИмяДампа = "dump.mdmp"; //ДанныеОтчетаОбОшибке.Отчет["dump"]["file"];

	Для Каждого ЭФайл Из ДанныеОтчетаОбОшибке.Файлы Цикл

		Если Новый Файл(Эфайл).Имя = ИмяДампа Тогда
			Возврат ЭФайл;
		КонецЕсли;
	КонецЦикла;

КонецФункции

Процедура ОтправитьФидбэк(ДанныеОтчетаОбОшибке)

	// https://develop.sentry.dev/sdk/envelopes/#user-feedback

	Отчет = ДанныеОтчетаОбОшибке.Отчет;

	Комментарий = Отчет["errorInfo"]["userDescription"];
	Если ПустаяСтрока(Комментарий) Тогда
		Возврат;
	КонецЕсли;

	ПарсерJSON = Новый ПарсерJSON();

	ПотокДанных = Новый ПотокВПамяти();
	ЗаписьДанных = Новый ЗаписьДанных(
		ПотокДанных,
		КодировкаТекста.UTF8,
		ПорядокБайтов.LittleEndian,
		"",
		"",
		Ложь);

	ТелоЗапросаJSON = Новый Соответствие();
	ТелоЗапросаJSON.Вставить("event_id", EventID(ДанныеОтчетаОбОшибке));
	
	ЗаписьДанных.ЗаписатьСтроку(ПарсерJSON.ЗаписатьJSON(ТелоЗапросаJSON, Ложь) + LF);

	ТелоЗапросаJSON = Новый Соответствие();
	ТелоЗапросаJSON.Вставить("type", "user_report");
	
	ЗаписьДанных.ЗаписатьСтроку(ПарсерJSON.ЗаписатьJSON(ТелоЗапросаJSON, Ложь) + LF);

	ТелоЗапросаJSON = Новый Соответствие();
	ТелоЗапросаJSON.Вставить("event_id", EventID(ДанныеОтчетаОбОшибке));
	ТелоЗапросаJSON.Вставить("name", Отчет["sessionInfo"]["userName"]);
	ТелоЗапросаJSON.Вставить("comments", Комментарий);

	ЗаписьДанных.ЗаписатьСтроку(ПарсерJSON.ЗаписатьJSON(ТелоЗапросаJSON, Ложь));

	POST_ENDPOINT("/envelope/", ПотокДанных.ЗакрытьИПолучитьДвоичныеДанные());

	ЗаписьДанных.Закрыть();

КонецПроцедуры

Процедура ОтправитьФайлы(ДанныеОтчетаОбОшибке)

	// https://develop.sentry.dev/sdk/envelopes/

	ПарсерJSON = Новый ПарсерJSON();

	maxAttachmentSize = 20 * 1024 * 1024;

	Для Каждого ФайлПуть Из ДанныеОтчетаОбОшибке.Файлы Цикл

		ФайлИнфо = Новый Файл(ФайлПуть);
		
		Если ФайлИнфо.Размер() > maxAttachmentSize Тогда
			Продолжить;
		КонецЕсли;

		ПотокДанных = Новый ПотокВПамяти();
		ЗаписьДанных = Новый ЗаписьДанных(
			ПотокДанных,
			КодировкаТекста.UTF8,
			ПорядокБайтов.LittleEndian,
			"",
			"",
			Ложь);

		ТелоЗапросаJSON = Новый Соответствие();
		ТелоЗапросаJSON.Вставить("event_id", EventID(ДанныеОтчетаОбОшибке));
		
		ЗаписьДанных.ЗаписатьСтроку(ПарсерJSON.ЗаписатьJSON(ТелоЗапросаJSON, Ложь) + LF);

		ТелоЗапросаJSON = Новый Соответствие();
		ТелоЗапросаJSON.Вставить("type", "attachment");
		ТелоЗапросаJSON.Вставить("filename", ФайлИнфо.Имя);
	
		ЗаписьДанных.ЗаписатьСтроку(ПарсерJSON.ЗаписатьJSON(ТелоЗапросаJSON, Ложь) + LF);

		// https://develop.sentry.dev/sdk/envelopes/#full-examples
		// TODO тут надо эскейпить 

		// 1. Length-prefixed payloads must terminate with \n or EOF
		// 2. If an Item with implicit length is terminated by \r\n,
		// then \r is considered an arbitrary character not part of
		// the newline, and thus part of the payload.
		// 3. Newlines are explicitly marked with \n,
		// unprintable characters are escaped with \x<><>.
		// all other characters are literal.

		ЗаписьДанных.Записать(Новый ДвоичныеДанные(ФайлИнфо.ПолноеИмя));

		// TODO Раскрыть после эскейпа
		
		// POST_ENDPOINT("/envelope/", ПотокДанных.ЗакрытьИПолучитьДвоичныеДанные());

		ЗаписьДанных.Закрыть();

	КонецЦикла;
	
КонецПроцедуры

Функция EventID(ДанныеОтчетаОбОшибке)

	Возврат СтрЗаменить(ДанныеОтчетаОбОшибке.Идентификатор, "-", ""); 

КонецФункции

Процедура POST_ENDPOINT(ENDPOINT, ТелоЗапроса, Скриншот = Неопределено, Минидамп = Неопределено)

	// https://develop.sentry.dev/sdk/overview/#authentication
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-Sentry-Auth", СтрШаблон("Sentry sentry_version=7,sentry_key=%1", PUBLIC_KEY));

	// https://develop.sentry.dev/sdk/overview/#parsing-the-dsn
	// https://develop.sentry.dev/sdk/store/
	//
	// '{BASE_URI}/api/{PROJECT_ID}/{ENDPOINT}/'

	URI = BASE_URI + "/api/" + PROJECT_ID + ENDPOINT;

	Параметры = Новый Структура("Заголовки", Заголовки);

	Файлы = Новый Массив();

	Если ЗначениеЗаполнено(Минидамп) Тогда

		Лог.Информация(Минидамп);
		Лог.Информация(Скриншот);

		Файл = Новый Структура;
		Файл.Вставить("Имя", "upload_file_minidump");
		Файл.Вставить("ИмяФайла", Новый Файл(Минидамп).Имя);
		Файл.Вставить("Данные", Новый ДвоичныеДанные(Минидамп));
		
		Файлы.Добавить(Файл);

		Файл = Новый Структура;
		Файл.Вставить("Имя", "upload_file_screenshot");
		Файл.Вставить("ИмяФайла", Новый Файл(Скриншот).Имя);
		Файл.Вставить("Данные", Новый ДвоичныеДанные(Скриншот));
		
		Файлы.Добавить(Файл);

		Параметры.Вставить("Файлы", Файлы);

		Параметры.Вставить("Данные", Новый Структура("sentry", ТелоЗапроса));
		
	КонецЕсли;

	Попытка

		Если Файлы.Количество() > 0 Тогда
			РезультатЗапроса = КоннекторHTTP.Post(URI, , , Параметры);
		Иначе
			РезультатЗапроса = КоннекторHTTP.Post(URI, ТелоЗапроса, , Параметры);
		КонецЕсли;
	Исключение
		Если ТипЗнч(ТелоЗапроса) = Тип("ДвоичныеДанные") Тогда
			ТелоЗапроса = Лев(ПолучитьСтрокуИзДвоичныхДанных(ТелоЗапроса), 200);
		КонецЕсли;
		Лог.Информация(ТелоЗапроса);
		Лог.Ошибка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;

	Если РезультатЗапроса.КодСостояния <> HTTP_CODE_OK_200 Тогда
		Если ТипЗнч(ТелоЗапроса) = Тип("ДвоичныеДанные") Тогда
			ТелоЗапроса = Лев(ПолучитьСтрокуИзДвоичныхДанных(ТелоЗапроса), 200);
		КонецЕсли;
		Лог.Информация(ТелоЗапроса);
		Лог.Ошибка("Status Code: " + РезультатЗапроса.КодСостояния);
		Лог.Ошибка(РезультатЗапроса.Текст());
		ВызватьИсключение("Не удалось отправить событие в Sentry");
	КонецЕсли;

КонецПроцедуры

Функция СформироватьТелоЗапроса(ДанныеОтчетаОбОшибке) Экспорт

	// https://develop.sentry.dev/sdk/event-payloads/

	Отчет = ДанныеОтчетаОбОшибке.Отчет;
	ДанныеИсключения = ИзвлечьДанныеИсключения(Отчет);

	ТелоЗапросаJSON = Новый Соответствие();

	ТелоЗапросаJSON.Вставить("event_id", EventID(ДанныеОтчетаОбОшибке));
	ТелоЗапросаJSON.Вставить("timestamp", Отчет["time"]);
	ТелоЗапросаJSON.Вставить("platform", "other");
	ТелоЗапросаJSON.Вставить("level", "error"); // fatal
	ТелоЗапросаJSON.Вставить("logger", Логгер);
	ТелоЗапросаJSON.Вставить("transaction", ДанныеИсключения["module"]);
	// ТелоЗапросаJSON.Вставить("server_name", "");
	ТелоЗапросаJSON.Вставить("release", Отчет["configInfo"]["hash"]); // git SHA or semantic version
	ТелоЗапросаJSON.Вставить("tags", Метки(Отчет));
	ТелоЗапросаJSON.Вставить("environment", "production"); // staging
	ТелоЗапросаJSON.Вставить("modules", Расширения(Отчет));
	ТелоЗапросаJSON.Вставить("extra", ДополнительнаяИнформация(Отчет));
	ТелоЗапросаJSON.Вставить("fingerprint", Отпечаток(ТелоЗапросаJSON, ДанныеИсключения));
	// ТелоЗапросаJSON.Вставить("errors", Новый Массив());
	ТелоЗапросаJSON.Вставить("exception", СформироватьИсключение(ДанныеИсключения));
	ТелоЗапросаJSON.Вставить("contexts", СформироватьКонтекст(Отчет));
	ТелоЗапросаJSON.Вставить("user", СформироватьПользователя(Отчет));
	
	ПарсерJSON = Новый ПарсерJSON();
	ТелоЗапроса = ПарсерJSON.ЗаписатьJSON(ТелоЗапросаJSON);

	Возврат ТелоЗапроса;

КонецФункции

Функция ИзвлечьДанныеИсключения(Отчет)

	// https://develop.sentry.dev/sdk/event-payloads/exception/

	Ошибка = Отчет["errorInfo"]["applicationErrorInfo"]["errors"][0];

	ТекстОшибки = Ошибка[0];
	ТипыОшибки = Ошибка[1];

	Если СтрНайти(ТекстОшибки, ":") Тогда
		ЧастиПредставленияОшибки = СтрРазделить(ТекстОшибки, ":");
		МодульОшибки = ЧастиПредставленияОшибки[0];
		МодульОшибки = СтрЗаменить(МодульОшибки, "{", "");
		МодульОшибки = СтрЗаменить(МодульОшибки, "}", "");
		ПредставлениеОшибки = СокрЛП(ЧастиПредставленияОшибки[1]);
	Иначе
		ПредставлениеОшибки = ТекстОшибки;
		МодульОшибки = "";
	КонецЕсли;
	
	// обрабатывается пока только первый тип, но их может быть несколько
	ТипОшибки = "";
	Если ТипыОшибки.Количество() Тогда
		ТипОшибки = ТипыОшибки[0];
	КонецЕсли;

	ДанныеИсключения = Новый Структура();
	ДанныеИсключения.Вставить("type", ТипОшибки);
	ДанныеИсключения.Вставить("value", ПредставлениеОшибки);
	ДанныеИсключения.Вставить("module", МодульОшибки);
	ДанныеИсключения.Вставить("stacktrace", ИзвлечьДанныеСтека(Отчет));

	Возврат ДанныеИсключения;

КонецФункции

Функция ИзвлечьДанныеСтека(Отчет)

	// https://develop.sentry.dev/sdk/event-payloads/stacktrace/

	ДанныеСтека = Отчет["errorInfo"]["applicationErrorInfo"]["stack"];

	Если ДанныеСтека = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Результат = Новый Массив();

	Для Каждого ЭлементСтека Из ДанныеСтека Цикл

		Лист = Новый Структура();
		Лист.Вставить("in_app", Истина);
		Лист.Вставить("function", ЭлементСтека[0]);
		Лист.Вставить("lineno", ЭлементСтека[1]);
		Лист.Вставить("context_line", ЭлементСтека[2]);

		Результат.Добавить(Лист);

	КонецЦикла;

	Возврат Новый Структура("frames", Результат);

КонецФункции

Функция Метки(Отчет)

	Метки = Новый Соответствие();
	Возврат Метки;

КонецФункции

Функция СформироватьИсключение(ДанныеИсключения)

	Исключения = Новый Массив;
	Исключения.Добавить(ДанныеИсключения);

	Результат = Новый Соответствие();
	Результат.Вставить("values", Исключения);

	Возврат Результат;

КонецФункции

Функция СформироватьКонтекст(Отчет)

	// https://develop.sentry.dev/sdk/event-payloads/contexts/

	ДанныеУстройство = Новый Структура();
	ДанныеУстройство.Вставить("family", "Desktop");
	ДанныеУстройство.Вставить("arch", ИзвлечьArch(Отчет["clientInfo"]["platformType"]));
	ДанныеУстройство.Вставить("name", Отчет["clientInfo"]["systemInfo"]["clientID"]);
	ДанныеУстройство.Вставить("manufacturer", Отчет["clientInfo"]["systemInfo"]["processor"]);
	ДанныеУстройство.Вставить("memory_size", Отчет["clientInfo"]["systemInfo"]["fullRAM"]);
	ДанныеУстройство.Вставить("free_memory", Отчет["clientInfo"]["systemInfo"]["freeRAM"]);

	ДанныеОС = Новый Структура;
	ДанныеОС.Вставить("name", ИзвлечьOSName(Отчет["clientInfo"]["platformType"]));
	ДанныеОС.Вставить("version", ИзвлечьOSVersion(Отчет["clientInfo"]["systemInfo"]["osVersion"]));

	ДанныеПлатформа = Новый Структура();
	ДанныеПлатформа.Вставить("name", Отчет["clientInfo"]["appName"]);
	ДанныеПлатформа.Вставить("version", Отчет["clientInfo"]["appVersion"]);

	ДанныеКонфигурции = Новый Структура();
	ДанныеКонфигурции.Вставить("app_identifier", Отчет["configInfo"]["name"]);
	ДанныеКонфигурции.Вставить("app_name", Отчет["configInfo"]["description"]);
	ДанныеКонфигурции.Вставить("app_version", Отчет["configInfo"]["version"]);
	ДанныеКонфигурции.Вставить("app_build", Отчет["configInfo"]["hash"]);

	Результат = Новый Структура();
	Результат.Вставить("device", ДанныеУстройство);
	Результат.Вставить("os", ДанныеОС);
	Результат.Вставить("runtime", ДанныеПлатформа);
	Результат.Вставить("app", ДанныеКонфигурции);
	
	Возврат Результат;

КонецФункции

Функция ИзвлечьArch(platformType)

	Возврат ?(СтрЗаканчиваетсяНа(platformType, "x86"), "x86", "x86_64");

КонецФункции

Функция ИзвлечьOSName(Знач platformType)

	platformType = НРег(platformType);

	Если СтрНачинаетсяС(platformType, "windows") Тогда
		Возврат "Windows";
	ИначеЕсли СтрНачинаетсяС(platformType, "macos") Тогда
		Возврат "macOS";
	ИначеЕсли СтрНачинаетсяС(platformType, "linux") Тогда
		Возврат "Linux";
	Иначе
		Возврат "Unknown";
	КонецЕсли;

КонецФункции

Функция ИзвлечьOSVersion(Знач Строка)

	// "osVersion": "Microsoft Windows 10 version 10.0  (Build 19042)"
	// 10.0.19041

	Строка = НРег(Строка);
	Строка = СтрЗаменить(Строка, "microsoft", "");
	Строка = СтрЗаменить(Строка, "windows 10", "");
	Строка = СтрЗаменить(Строка, "version", "");
	Строка = СтрЗаменить(Строка, "build", ".");
	Строка = СтрЗаменить(Строка, "(", "");
	Строка = СтрЗаменить(Строка, ")", "");
	Строка = СтрЗаменить(Строка, " ", "");

	Возврат Строка; 

КонецФункции

Функция СформироватьПользователя(Отчет)

	УникальныйИдентификаторПользователя = Отчет["sessionInfo"]["userName"];
	Если Не ПустаяСтрока(Отчет["sessionInfo"]["dataSeparation"]) Тогда
		УникальныйИдентификаторПользователя = УникальныйИдентификаторПользователя
			+ Отчет["sessionInfo"]["dataSeparation"];
	КонецЕсли;

	// Надо еще замешать айди информационной базы, если сбор с нескольких

	Результат = Новый Структура();
	Результат.Вставить("id", УникальныйИдентификаторПользователя);
	Результат.Вставить("username", Отчет["sessionInfo"]["userName"]);

	Возврат Результат;

КонецФункции

Функция Расширения(Отчет)

	Результат = Новый Соответствие();

	БлокРасширения = Отчет["configInfo"]["extentions"];
	Если БлокРасширения = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Расширение Из БлокРасширения Цикл

		Результат.Вставить(Расширение[0], Расширение[1]);

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ДополнительнаяИнформация(Отчет)

	Результат = Новый Соответствие();
	Результат.Вставить("compatibilityMode", Отчет["configInfo"]["compatibilityMode"]);
	Результат.Вставить("changeEnabled", Отчет["configInfo"]["changeEnabled"]);

	Возврат Результат;

КонецФункции

Функция Отпечаток(ТелоЗапросаJSON, ДанныеИсключения)

	Результат = Новый Массив;

	Результат.Добавить(ТелоЗапросаJSON["release"]);
	Результат.Добавить(ДанныеИсключения["module"]);
	Результат.Добавить(ДанныеИсключения["value"]);

	Возврат Результат;

КонецФункции

HTTP_CODE_OK_200 = 200;
LF = Символ(10);
